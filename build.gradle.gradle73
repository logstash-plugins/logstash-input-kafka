import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING
/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
apply plugin: "java"
apply plugin: 'maven-publish'
apply plugin: "distribution"
apply plugin: "idea"

group "org.logstash.inputs"

sourceCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.apache.kafka:kafka-clients:2.8.1'
    compileOnly 'com.github.luben:zstd-jni:1.5.0-2'
    compileOnly 'org.slf4j:slf4j-api:1.7.30'
    compileOnly 'org.lz4:lz4-java:1.7.1'
    compileOnly 'org.xerial.snappy:snappy-java:1.1.8.1'

}

configurations {
    compileOnlyClasspath {
        // https://discuss.gradle.org/t/what-is-a-configuration-which-cant-be-directly-resolved/30721
        extendsFrom compileOnly
        canBeConsumed false
        canBeResolved true
    }
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-input-kafka_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.compileOnlyClasspath.allDependencies.each { dep ->
            File f = configurations.compileOnlyClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
